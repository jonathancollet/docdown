
/*
 * Copyright (C) 2013-2013 Nicolas Christe
 * Copyright (C) 2013-2013 Parrot S.A.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 *
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'java'

// In this section you declare where to find the dependencies of your project
repositories {
    mavenCentral()
    maven {
        url "http://rendersnake.googlecode.com/svn/repo"
    }
    mavenLocal()
}

// configuration for "compile only dependencies"
configurations {
    provided
    provided.extendsFrom(compile)
}

sourceCompatibility = JavaVersion.VERSION_1_7

dependencies {
    provided files("${System.properties['java.home']}/../lib/tools.jar")
    compile "org.rendersnake:rendersnake:1.7.3"
    compile "org.pegdown:pegdown:1.4.2"
}

sourceSets {
    main {
        // put resources in the code classpath
        output.resourcesDir = output.classesDir
        // add provided configuration to the compile classpath
        compileClasspath += configurations.provided
    }

    // add sourceset for sample code included in the documentation
    sample {
    }
 }

jar {
    // build a "fat" jar
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    // remove all signature files
    exclude "META-INF/*.SF"
    exclude "META-INF/*.DSA"
    exclude "META-INF/*.RSA"
}

